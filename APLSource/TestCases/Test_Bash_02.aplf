 R←Test_Bash_02(stopFlag batchFlag);⎕TRAP;url_1;url_2;r;DM
⍝ Exercise these functions: `CurrentBranch`, `ListBranches`, `CreateBranch`, `DeleteBranch`, `Checkout`
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 :Trap 11
     {}#.Proj_1 B.DeleteBranch'dev'  ⍝ Just in case
 :EndTrap
 #.Proj_1 B.CreateBranch'dev'
 →PassesIf'dev'≡B.CurrentBranch #.Proj_1
 #.Proj_1 B.CreateBranch'to_be_deleted'
 →PassesIf'to_be_deleted'≡B.CurrentBranch #.Proj_1
 →PassesIf 3=≢B.ListBranches #.Proj_1
 :Trap 11
     r←#.Proj_1 B.DeleteBranch'to_be_deleted'
     →FailsIf 1
 :Else
     DM←⎕DMX.DM
     →PassesIf∨/'The current branch cannot be deleted'⍷∊DM
 :EndTrap
 r←#.Proj_1 B.CheckOut'dev'
 →PassesIf 0=≢r
 r←#.Proj_1 B.DeleteBranch'to_be_deleted'
 →PassesIf'Deleted branch to_be_deleted'{⍺≡(⍴⍺)⍴⍵}⊃r
 →PassesIf 2=≢B.ListBranches #.Proj_1
 r←#.Proj_1 B.CheckOut'master'
 →PassesIf 0=≢r
 r←#.Proj_1 B.DeleteBranch'dev'
 →PassesIf∨/'Deleted branch dev'⍷∊r

 R←∆OK
⍝Done
