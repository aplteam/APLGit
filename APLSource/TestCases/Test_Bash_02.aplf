 R←Test_Bash_02(stopFlag batchFlag);⎕TRAP;url_1;url_2;r;DM
⍝ Exercise these functions: `CurrentBranch`, `ListBranches`, `CreateBranch`, `DeleteBranch`, `Checkout`
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 :Trap 11
     {}#.Proj_1 B.DeleteBranch'dev'  ⍝ Just in case
 :EndTrap
 #.Proj_1 B.CreateBranch'dev'
 →T.PassesIf'dev'≡B.CurrentBranch #.Proj_1
 #.Proj_1 B.CreateBranch'to_be_deleted'
 →T.PassesIf'to_be_deleted'≡B.CurrentBranch #.Proj_1
 →T.PassesIf 3=≢B.ListBranches #.Proj_1
 :Trap 11
     r←#.Proj_1 B.DeleteBranch'to_be_deleted'
     →T.FailsIf 1
 :Else
     DM←⎕DMX.DM
     →T.PassesIf∨/'The current branch cannot be deleted'⍷∊DM
 :EndTrap
 r←#.Proj_1 B.CheckOut'dev'
 →T.PassesIf 0=r
 r←#.Proj_1 B.DeleteBranch'to_be_deleted'
 →T.PassesIf'Deleted branch to_be_deleted'{⍺≡(⍴⍺)⍴⍵}⊃r
 →T.PassesIf 2=≢B.ListBranches #.Proj_1
 r←#.Proj_1 B.CheckOut'master'
 →T.PassesIf 0=r
 r←#.Proj_1 B.DeleteBranch'dev'
 →T.PassesIf∨/'Deleted branch dev'⍷∊r

 R←T._OK
⍝Done
