 R←Test_Bash_03(stopFlag batchFlag);⎕TRAP;url_1;url_2;r;project;folder;status;DM
⍝ Exercise these functions: `IsDirty`, `Commit`, `Status`, `StatusShort`, `CheckOut`, `Tags`, `Log`, `DeleteBranch`
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 project←B.EstablishProject #.Proj_1
 folder←2⊃project
 :Trap 6
     {}folder B.CheckOutBranch'dev'
     →T.FailsIf 1
 :Else
     →T.PassesIf'Branch does not exist'≡⊃⎕DMX.DM
 :EndTrap

 r←folder B.CreateBranch'HotFix_512'
 →T.PassesIf 0=≢r
 →T.PassesIf'HotFix_512'≡B.CurrentBranch folder
 →T.PassesIf 0=B.IsDirty folder
 A.WriteUtf8File(folder,'file1.txt')'Hello world in HotFix_512'
 →T.PassesIf 1=B.IsDirty folder
 r←folder B.Commit''
 r←folder B.CreateBranch'dev'
 →T.PassesIf 0=≢r
 →T.PassesIf'dev'≡B.CurrentBranch folder
 →T.PassesIf 0=B.IsDirty folder
 A.WriteUtf8File(folder,'file1.txt')'Hello universe in "dev"'
 A.WriteUtf8File(folder,'file2.txt')'The answer is 42 in "dev"!'
 →T.PassesIf 1=B.IsDirty folder
 status←B.Status folder
 →T.PassesIf 1 1≡+/¨'file1.txt' 'file2.txt'⍷¨⊂∊status
 r←folder B.Commit'Firlefanz'
 →T.PassesIf 2=≢r
 →T.PassesIf'dev'≡1⊃r
 →T.PassesIf 0=B.IsDirty folder
 status←B.StatusShort folder
 →T.PassesIf 0=≢status
 r←B.Tags folder
 →T.PassesIf 0=≢r
 r←folder B.Log 0
 →T.PassesIf 2=≢r
 →T.PassesIf 1=+/'Firlefanz'⍷∊' ',¨r[;4]
 r←folder B.DeleteBranch'HotFix_512'
 →T.PassesIf 1
 :Trap 11
     r←folder B.DeleteBranch'dev'
     →T.FailsIf 1
 :Else
     DM←⎕DMX.DM
     →T.PassesIf∨/'The current branch cannot be deleted'⍷∊DM
 :EndTrap
 r←folder B.CheckOutBranch'master'
 →T.PassesIf 0=r
 r←folder B.DeleteBranch'dev'
 →T.PassesIf r

 R←T._OK
⍝Done
