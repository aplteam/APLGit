 dir←SetDefaultDir y;project
⍝ Allows you to set a default dir in case you don't want `APLGit` to use the current directory.\\
⍝ If `y` is empty the current default directory is returned or an empty vector if none is defined.\\
⍝ If `y` is not empty it must be either the fully qualified name of a space (starting with `#`)
⍝ or a folder pointing to an acre project.\\
 :If 0=≢y
     :If 0=##.⎕NC'∆DefaultDir'
     :OrIf 0=≢##.∆DefaultDir
         dir←''
     :Else
         dir←##.∆DefaultDir
     :EndIf
 :Else
     y←⍕y
     :If '#'=1⍴y
         project←##.EstablishProject y
         'Not a project'⎕SIGNAL 6/⍨0=≢project
         ##.∆DefaultDir←dir←2⊃project
     :Else
         ##.∆DefaultDir←dir←y
     :EndIf
 :EndIf
⍝Done
