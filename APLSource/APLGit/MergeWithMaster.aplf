 {r}←MergeWithMaster y;project;folder;currentBranch;gitMsg;space
⍝ Merge `currentBranch` (which **must not** be "master") with the master.\\
⍝ Call `Squash` beforehand in order to clean up the commit history.\\
⍝ For what `y` can actually be see `EstablishProject`.\\
 project←EstablishProject y
 'No open acre projects'⎕SIGNAL 6/⍨0=≢project
 (space folder)←project[1 2]
 currentBranch←GetCurrentBranch folder
 'This command MUST NOT be executed in the "master" currentBranch'⎕SIGNAL 11/⍨'master'≡currentBranch
 ('Branch "',currentBranch,'" is dirty!')⎕SIGNAL 11/⍨IsDirty folder
 ⎕←⊃folder U.RunGitCommand'fetch origin master:master'
 :If ~IsRebased folder
     11 ⎕SIGNAL⍨'Not merged! ("',currentBranch,'" not rebased to master)'
 :EndIf
 ⎕←⊃folder U.RunGitCommand'checkout master'
 gitMsg←⊃folder U.RunGitCommand'merge --ff-only ',currentBranch
 'Fast-forward merge impossible, settle for ordinary merge!'⎕SIGNAL 11/⍨'fatal:'{⍺≡(⍴⍺)↑⍵}gitMsg
 ⎕←⊃folder DeleteBranch currentBranch
 ⎕←⊃1 Push folder
 ⎕SE.UCMD'acre.Close ',space
 #.⎕EX space
 ⎕←'Project closed and deleted'
⍝Done
