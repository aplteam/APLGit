 r←{x}DiffBetween y;buff;folder;project;d1;d2;rc;more
⍝ Lists all file differences between `y`.\\
⍝ * In case `y` is empty it defaults to "master" and "dev".
⍝ * If it is not empty it must be a two-element vector.\\
⍝   The two items can each specify a branch but also a commit-hash.\\
⍝ For what `x` can actually be see `EstablishProject`.\\
 x←{0<⎕NC ⍵:⍎⍵ ⋄ ''}'x'
 project←EstablishProject x
 'Not an acre project'⎕SIGNAL 11/⍨0=≢project
 folder←2⊃project
 :If 0=≢y
     (d1 d2)←'master' 'dev'
 :Else
     (d1 d2)←y
 :EndIf
 (rc more buff)←folder U.RunGitCommand2'diff --name-status ',d1,'..',d2
 U.Assert 0=rc
 r←↑{i←⍵⍳' ' ⋄ ((i-1)↑⍵)(A.DLB i↓⍵)}¨buff
⍝Done
