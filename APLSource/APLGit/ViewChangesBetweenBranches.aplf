 {r}←{x}ViewChangesBetweenBranches y;tfn1;tfn2;project;folder;branch1;branch2;branchHashes;firstBranchHash;allHashes;list;modified;thisObj;name;header;now
⍝ Allows the inspection of all changes in the two entities defined by `y`.\\
⍝ `y` might be one of:
⍝ * Empty vector; in that case it defaults to "master" and "dev".
⍝ * A two-element vector each of which is a text vector with a branch name.\\
⍝ For the actual comparison the user command `]CompareFiles` is used.\\
⍝ For what `x` can actually be see `EstablishProject`.\\
⍝ Returns always `⍬`.\\
 x←{0<⎕NC ⍵:⍎⍵ ⋄ ''}'x'
 r←⍬
 tfn1←tfn2←''
 project←EstablishProject x
 'Not an acre project'⎕SIGNAL 11/⍨0=≢project
 folder←2⊃project
 :If 0=≢y
     (branch1 branch2)←'master' 'dev'
 :Else
     (branch1 branch2)←y
 :EndIf
 branchHashes←folder LogForBranch branch2
 →(0=≢branchHashes)/0
 firstBranchHash←⊃¯1↑branchHashes
 allHashes←,1↑[2]Log folder
 compareWith←(1+allHashes⍳⊂firstBranchHash)⊃allHashes
 list←folder DiffBetween branch1 branch2
 :If 0=+/modified←list[;1]≡¨⊂,'M'
     ∆TestFlag U.Show'No changes found'
     :Return
 :EndIf
 tfn1←'txt'F.GetTempFilename2''
 tfn2←'txt'F.GetTempFilename2''
 :For thisObj :In modified⌿list[;2]
     name←⊃⎕SE.acreAPI.itemname thisObj
     header←{↓⍵,[0.5]'='⍴⍨⍴⍵}name
     :If (⎕NC name)∊3 4 9           ⍝ 2    : Vars not (yet?!) supported  ⍝TODO⍝
         :If 3 4∊⍨⎕NC name
             now←header,⎕NR name
         :ElseIf 9=⎕NC name
             now←header,⎕SRC⍎name
         :ElseIf 2=⎕NC name
             :Continue              ⍝ For the time being variables are ignored.
         :EndIf
         'append'A.WriteUtf8File tfn1(header,folder U.RunGitCommand'show ',compareWith,':',thisObj)
         'append'A.WriteUtf8File tfn2 now
     :EndIf
 :EndFor
 {}⎕SE.UCMD'CompareFiles "',tfn1,'" "',tfn2,'" -ro1=1 -ro2=1 -label1=''',branch1,''' -label2=''',branch2,''''
 F.DeleteFile¨tfn1 tfn2
⍝Done
