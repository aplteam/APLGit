 {r}←{msg}Initialize y;project;folder;rc;more;buff
⍝ Makes `y` a Git Repository and commits what's there as "Initial commit".\\
⍝ After this use GitHub to create a new repository, then get the URL and
⍝ run `AddToGitHub` with the URL as right argument.\\
⍝ For what `y` can actually be see `EstablishProject`.\\
 msg←{0<⎕NC ⍵:⍎⍵ ⋄ 'Initial commit'}'msg'
 project←EstablishProject y
 'Not an acre project'⎕SIGNAL 6/⍨0=≢project
 folder←2⊃project
 (rc more buff)←folder U.RunGitCommand2'init'
 U.Assert 0=rc
 ∆TestFlag U.Show buff
 A.WriteUtf8File(folder,'.gitattributes')U.GetGitAttributes
 (rc more buff)←folder U.RunGitCommand2'add .'
 U.Assert 0=rc
 ∆TestFlag U.Show buff
 msg←U.MassageMsg msg
 (rc more buff)←folder U.RunGitCommand2'commit -m "',msg,'"'
 U.Assert 0=rc
 ∆TestFlag U.Show buff
 r←⍬
⍝Done
