 {r}←{noPrompt}Push y;project;branchName;folder
⍝ Pushes all committed changes to GitHub.\\
⍝ The user will be prompted unless the (optional) left argument is 1.\\
⍝ For what `y` can actually be see `EstablishProject`.\\
 r←⍬
 noPrompt←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'noPrompt'
 :If 0=≢project←1 EstablishProject y
     'Not an acre project'⎕SIGNAL 6
 :Else
     folder←2⊃project
     branchName←GetCurrentBranch folder
     :If noPrompt
     :OrIf 0=U.YesOrNo'You are on branch <',branchName,'> in ',folder,' - carry on with "Push"?'
         ∆TestFlag U.Show'No action taken'
         :Return
     :EndIf
     ∆TestFlag U.Show folder U.RunGitCommand'push origin ',branchName
 :EndIf
⍝Done
