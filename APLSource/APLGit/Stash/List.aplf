 r←List y;project;cmd;folder;buff
⍝ Lists all stashes saved for `folder`.\\
⍝ Returns a matrix with zero or more lines and three cols:
⍝ 1. Internal stash ID
⍝ 2. Branch name the stash was taken from
⍝ 3. The name of the stash assigned by the user
⍝ `y` can be a folder, empty (the current dir rules) or a space (starting with `#`).
 project←##.EstablishProject y
 'Not an acre project'⎕SIGNAL 11/⍨0=≢project
 folder←2⊃project
 cmd←'stash list'
 r←folder U.RunGitCommand cmd
 :If 0=≢r
     r←0 3⍴''
 :Else
     buff←{':'#.APLTreeUtils.Split ⍵}¨r
     r←↑{(1⊃⍵)(4↓2⊃⍵)({⍵↓⍨+/∧\' '=⍵}3⊃⍵)}¨buff
 :EndIf
⍝Done
