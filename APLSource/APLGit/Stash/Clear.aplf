 r←{force}Clear y;project;cmd;folder
⍝ Clears all stashes. There is no Undo!\\
⍝ By default the user is prompted. You can suppress this by specifying a 1 as left argument.\\
⍝ `y` can be a folder, empty (the current dir rules) or a space (starting with `#`).
 force←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'force'
 '"force" must be a Boolean'⎕SIGNAL 11/⍨~(⊂force)∊0 1
 project←##.EstablishProject y
 'Not an acre project'⎕SIGNAL 11/⍨0=≢project
 folder←2⊃project
 :If 0<r←≢List folder
     :If force
     :OrIf U.YesOrNo'Are you sure that you want to throw away all stashes?'
         cmd←'stash clear'
         folder U.RunGitCommand cmd
     :EndIf
 :EndIf
⍝Done
