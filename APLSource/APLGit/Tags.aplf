 r←{detailed}Tags y;project;folder;rc;more
⍝ Returns a vector if text vectors with the tag assigned to any commit.\\
⍝ In case the optional left argument is 1 (default is 0) instead a matrix
⍝ with 0 to many rows and three columns is returned:
⍝ 1. The tags as such
⍝ 2. Numeric three-element vector per tag (version number)
⍝ 3. Character representation of the numeric version number\\
⍝ The result is sorted from latest to oldest.\\
⍝ For what `y` can actually be see `EstablishProject`.\\
 detailed←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'detailed'
 :If 0≠≢project←EstablishProject y
     folder←2⊃project
     (rc more r)←folder U.RunGitCommand2'tag'
     U.Assert 0=rc
     :If detailed
         r←r,[1.5]U.FormatVersion¨r
         r,←U.FormatVersion¨r[;2]
         r←r[⍒↑r[;2];]
     :Else
         r←r[⍒↑r]
     :EndIf
 :EndIf
⍝Done
