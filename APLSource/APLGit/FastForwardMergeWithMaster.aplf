 {r}←{noPrompt}FastForwardMergeWithMaster y;project;folder;currentBranch;gitMsg;space
⍝ Merge `currentBranch` (which **must not** be "master") with the master.\\
⍝ This is a fast-forward merge; that means it will only work if the branch stems from the branch.
⍝ If that's not the case (for example because you have fetched other people's work and therefore
⍝ changed your master branch) then use `MergeWithMaster` instead.
⍝ Call `Squash` beforehand in order to clean up the commit history.\\
⍝ For what `y` can actually be see `EstablishProject`.\\
 noPrompt←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'noPrompt'
 project←EstablishProject y
 'No open acre projects'⎕SIGNAL 6/⍨0=≢project
 (space folder)←project[1 2]
 currentBranch←CurrentBranch folder
 'This command MUST NOT be executed in the "master" currentBranch'⎕SIGNAL 11/⍨'master'≡currentBranch
 ('Branch "',currentBranch,'" is dirty!')⎕SIGNAL 11/⍨IsDirty folder
 'This function MUST be called from #'⎕SIGNAL 11/⍨(,'#')≢,⊃⎕NSI
 ⎕←'Now checking out master'
 ⎕←⊃folder U.RunGitCommand'checkout master'
 ('Branch "master" is dirty!')⎕SIGNAL 11/⍨IsDirty folder
 :If U.YesOrNo'Would like to pull "master"?'
     PullMaster folder
 :EndIf
 gitMsg←⊃folder U.RunGitCommand'merge --ff-only ',currentBranch
 'Fast-forward merge impossible, settle for ordinary merge!'⎕SIGNAL 11/⍨'fatal:'{⍺≡(⍴⍺)↑⍵}gitMsg
 ⎕←'Merged'
 ⎕←⊃folder DeleteBranch currentBranch
 {}⎕SE.UCMD'acre.Close ',space
 #.⎕EX space
 ⎕←'Project closed, namespace "',space,'" and branch "',currentBranch,'" deleted'
 :If noPrompt
 :OrIf U.YesOrNo'Would you like to push the master?'
     gitMsg←1 Push folder
     :If ∨/∨/¨'fatal' 'error'⍷¨⊂∊gitMsg
         ⎕←'*** "Push" failed!'
         ⎕←⍪gitMsg
     :Else
         ⎕←'Pushed to GitHub'
     :EndIf
 :EndIf
⍝Done
