 {r}←{suppressOutput}Fetch y;folder;project;buff;qdmx;rc;more
⍝ Fetch latest changes from GitHub
⍝ For what `y` can actually be see `EstablishProject`.\\
 r←⍬
 :If 0≠≢project←EstablishProject y
     folder←2⊃project
     suppressOutput←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'suppressOutput'
     suppressOutput∨←∆TestFlag
     :Trap 11
         (rc more buff)←folder U.RunGitCommand2'fetch origin'
         U.Assert 0=rc
     :Else
         qdmx←⎕DMX
         :If ∨/'fatal: ''origin'' does not appear to be a git repository'⍷∊qdmx.DM
             'Apparently not on GitHub'⎕SIGNAL 11
         :Else
             ∘∘∘ ⍝ Huh?!
         :EndIf
     :EndTrap
     suppressOutput U.Show buff
     (rc more r)←folder U.RunGitCommand2'diff --name-status master origin/master'
     U.Assert 0=rc
     suppressOutput U.Show r
 :EndIf
⍝Done
