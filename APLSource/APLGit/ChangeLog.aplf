 r←{x}ChangeLog name;project;folder;filename;space
⍝ Takes the name of an APL object and returns a matrix with information about all commits that object was changed by.\\
⍝ The result is a matrix with zero or more rows and 4 columns:
⍝ 1. Hash
⍝ 2. Commiter's name
⍝ 3. Date of the commit date in strict ISO 8601 format
⍝ 4. Message of the commit\\
⍝ Note that this function honours renames (`--follow` parameter).\\
⍝ For what `x` can actually be see `EstablishProject`.\\
 x←{0<⎕NC ⍵:⍎⍵ ⋄ ''}'x'
 project←EstablishProject x
 'Not an acre project'⎕SIGNAL 11/⍨0=≢project
 (space folder)←2↑project
 filename←(⍴folder)↓⎕SE.acreAPI.sourcefile name
 r←folder U.RunGitCommand'log --follow --pretty=format:"',U.GetLogFormat,'" -- ',filename
 (⊃r)⎕SIGNAL 11/⍨'fatal:'{⍺≡(⍴⍺)↑⍵}⊃r
 :If 0=≢r
     r←0 4⍴''
 :Else
     r←↑U.FormatLog r
 :EndIf
⍝Done
